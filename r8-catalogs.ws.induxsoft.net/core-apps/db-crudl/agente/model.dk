#include "dkli.dkh"
#!

module "agente.dk"
{
    
    @read_query = "select sys_pk, sys_guid, sys_recver, sys_timestamp, sys_dtcreated, sys_deleted, 
    codigo,codnomina,pcomision,email,nombre,telefono,notas,domicilio1
    from agente where #<@keyfield>=@_entity_id limit 1;"


   	agente.read::&db,&params
    {

        ref agente=entity.read(db,params)
        new d
        {
            @"domicilio1":@@(agente,"#domicilio1")
        }

        agente<"domicilio*">:dbr.rec(db,"select codpos,colonia,direccion,notas,telefonos,sys_pk,sys_recver,iciudad from domicilio where sys_pk=@domicilio1",d)
        agente<"ciudad*">:dbr.rec(db,"select codigo,codigotel,nombre,sys_pk,sys_recver,Estado from ciudad where sys_pk=@iciudad",@@(agente,"&domicilio"))
        agente<"estado*">:dbr.rec(db,"select codigo,nombre,IPais,sys_pk,sys_recver from edoprov where sys_pk=@estado",@@(agente,"&ciudad"))
        agente<"pais*">:dbr.rec(db,"select codigo,codigotel,nombre,sys_pk,sys_recver from pais where sys_pk=@IPais",@@(agente,"&estado"))
        return  agente
    }

    agente.create::&db,&params,&data
    {
        @use_transaction=@false
		data<"sys_recver">:parse.num(@@(data,"#sys_recver"))
		data<"sys_pk">:parse.num(@@(data,"#sys_pk"))
    	do dbr.begin(db)
    		if not(isnull(@@(data,"&domicilio")))
	        {
				data<"domicilio/sys_recver">:parse.num(@@(data,"#domicilio/sys_recver"))
	            ref domicilio=dbr.save(db,"domicilio",@@(data,"&domicilio"))
	            data<"domicilio1">:@@(domicilio,"sys_pk")
	            do field.remove(data,"domicilio")
	        }
	        if not(isnull(@@(data,"&ciudad"))){do field.remove(data,"ciudad")}
	        if not(isnull(@@(data,"&estado"))){do field.remove(data,"estado")}
	        if not(isnull(@@(data,"&pais"))){do field.remove(data,"pais")}
	        ref res= entity.create(db,params,data)
    	do dbr.commit(db)
    	return res
    	exception
    	{
    		if dbr.in_transaction(db) {do dbr.rollback(db)}
    		do rise_error(500,last_error())
    	}
    }

    agente.update::&db,&params,&data
    {
        if not(isnull(@@(data,"&domicilio")))
    	{
            data<"domicilio/sys_recver">:parse.num(@@(data,"#domicilio/sys_recver"))
    		ref domicilio=dbr.save(db,"domicilio",@@(data,"&domicilio"))
            data<"domicilio1">:@@(domicilio,"sys_pk")
            do field.remove(data,"domicilio")
    	}
    	return entity.update(db,params,data)
    }

    agente.delete::&db,&params
    {	
        @keyfield="sys_pk"
        ref agente=entity.read(db,params)
        new d
        {
            @"domicilio1":@@(agente,"#domicilio1")
        }
        do dbr.delete(db,@table_name,"sys_pk=@_entity_id",params)
        do dbr.delete(db,"domicilio","sys_pk=@domicilio1",d)
    }
    entity.blank::&db,&params
	{
		new r
		{
			@"pcomision":0
		}

		return r
	}
    point @read to agente.read
    point @create to agente.create
    point @update to agente.update
    point @delete to agente.delete
}